apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "io.github.a0gajun.weather"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "OPEN_WEATHER_MAP_API_KEY", "\"${project.property("openWeatherMapApiKey")}\""
            buildConfigField "String", "GOOGLE_MAPS_API_KEY", "\"${project.property("googleMapsApiKey")}\""
            resValue "string", "google_geo_api_key", "\"${project.property("googleMapsApiKey")}\""
        }

        release {
            buildConfigField "String", "OPEN_WEATHER_MAP_API_KEY", "\"${project.property("openWeatherMapApiKey")}\""
            buildConfigField "String", "GOOGLE_MAPS_API_KEY", "\"${project.property("googleMapsApiKey")}\""
            resValue "string", "google_geo_api_key", "${project.property("googleMapsApiKey")}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

}

ext {
    // Android
    supportLibraryVersion = '25.1.0'
    googlePlayServiceVersion = '10.0.1'

    // Libraries
    lombokVersion = '1.16.12'
    daggerVersion = '2.8'
    rxAndroidVersion = '1.2.1'
    rxJavaVersion = '1.2.4'
    retrofitVersion = '2.1.0'
    gsonVersion = '2.8.0'
    eventBusVersion = '3.0.0'
    okHttpVersion = '3.5.0'
    threeTenAbpVersion = '1.0.4'
    glideVersion = '3.7.0'
    glideTransformationVersion = '2.0.1'
    lightweightStreamApiVersion = '1.1.4'
    apacheCommonsLangVersions = '3.5'
    ormaVersion = '3.2.2' // Using old version to use rxJava 1.x

    // Develop
    timberVersion = '4.4.0'
    leakCanaryVersion = '1.5'
    stethoVersion = '1.4.2'

    // Testing
    mockitoVersion = '2.5.5'
    robolectricVersion = '3.2.1'

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided "org.projectlombok:lombok:${lombokVersion}"

    ///////////////
    // Libraries //
    ///////////////

    // Support Library
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    // Google Play Service
    compile "com.google.android.gms:play-services-location:${googlePlayServiceVersion}"
    // Dagger : DI library
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    // RxAndroid : Functional Reactive programming library
    compile "io.reactivex:rxandroid:${rxAndroidVersion}"
    compile "io.reactivex:rxjava:${rxJavaVersion}"
    // Retrofit : REST library
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    // OkHttp : Networking library
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    // gson : json conversion library
    compile "com.google.code.gson:gson:${gsonVersion}"
    // Timber : logging library
    compile "com.jakewharton.timber:timber:${timberVersion}"
    // EventBus
    compile "org.greenrobot:eventbus:${eventBusVersion}"
    // ThreeTenABP : JSR-310 backport for android
    compile "com.jakewharton.threetenabp:threetenabp:${threeTenAbpVersion}"
    // Glide : Image loading framework
    compile "com.github.bumptech.glide:glide:${glideVersion}"
    compile "jp.wasabeef:glide-transformations:${glideTransformationVersion}"
    // Lightweight Stream Api : Stream API backporting library
    compile "com.annimon:stream:${lightweightStreamApiVersion}"
    // ApacheCommonsLang :
    compile group: 'org.apache.commons', name: 'commons-lang3', version: "${apacheCommonsLangVersions}"
    // Orma : ORM
    annotationProcessor "com.github.gfx.android.orma:orma-processor:${ormaVersion}"
    compile "com.github.gfx.android.orma:orma:${ormaVersion}"

    // LeakCanary : memory leak detection
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"

    // Stetho : A debug bridge for Android applications
    compile "com.facebook.stetho:stetho:${stethoVersion}"
    compile "com.facebook.stetho:stetho-okhttp3:${stethoVersion}"


    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
}

// Dagger workaround
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500" // or whatever number you want
    }
}
